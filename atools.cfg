//Admin tools by Infinity (made from the beginning since lack of compability between versions)

//Take admin on the servers you have admin with the same key

/goadmin (to take admin if you have it)
//akick (kick with admin)
//aban (ban with admin)
//abanss (aban with remember for info and screenshot)
//abanname (aban by the name)
//abannamess (abanname with remember for info and screenshot)
//set_abanname (to fastly ban trollers)

//Initialize things
checkinit servers_ips_and_ports
checkinit servers_names
checkinit servers_passwords

// SETTING UP SERVERS AND PASSWORDS
bind X [showmenu "\f0Add\f5 / \f1update\f5 this server \f3admin\f5"]

//Menu add server
newmenu "\f0Add\f5 / \f1update\f5 this server \f3admin\f5"
temp_server_password = ""
menuitemtextinput "Pass " [] [temp_server_password = $arg1]
menuitem "\f2[Add]" [
    closecurmenu

    if (strcmp $temp_server_password "") [
        temp_server_password = "-"
    ]

    if (< (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))) 0) [
        add2list servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3)); add2list servers_names (strreplace (curserver 6) " " "_"); add2list servers_passwords $temp_server_password; echo (curserver 6) " \f5("(concatword (curserver 1) ":" (curserver 3))") \f0Added correctly";
    ] [
        servers_names = (strreplace $servers_names (at $servers_names (findlist ($servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))))) (strreplace (curserver 6) " " "_"))
        servers_passwords = (strreplace $servers_passwords (at $servers_passwords (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3)))) $temp_server_password)

        echo "\f1Server updated successfully"
    ]
    ]


// ADMIN ON THE SERVERS YOU GOT WITH THE SAME KEY
goadmin = [
    if (>= (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))) 0) [
        //If pass is "-" then use claimadmin alone
        if (strcmp (at $servers_passwords (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3)))) "-") [
            claimadmin
        ] [
            temp_server_password = (at $servers_passwords (findlist $servers_ips_and_ports (concatword (curserver 1) ":" (curserver 3))))
            setadmin 1 $temp_server_password
        ]
    ] [
        //If you do not have admin
        echo "\f3You \f2don't have admin \f3rights on this server"
    ]
]

//ADMIN UTILITIES
checkinit temp_reason

admin_name = "ADMIN_NAME"

bind P [
    name $admin_name
    goadmin
    setadmin 0
]

//akick (kick with admin)
akick = [
    temp_cn_vote = $arg1
    temp_old_name = (player1 name)
    temp_reason = $arg2
    if (< (strlen $arg2) 4) [
        temp_reason = "Fast reason (probably troller, hacker or ban evade)"
    ]

    name $admin_name
    goadmin
    sleep 200 [kick $temp_cn_vote $temp_reason]
    sleep 300 [setadmin 0]
    sleep 300 [name $temp_old_name]
]

//aban (ban with admin)
aban = [
    temp_cn_vote = $arg1
    temp_old_name = (player1 name)
    temp_reason = $arg2
    if (< (strlen $arg2) 4) [
        temp_reason = "Fast reason (probably troller, hacker or ban evade)"
    ]

    name $admin_name
    goadmin
    sleep 200 [ban $temp_cn_vote $temp_reason]
    sleep 300 [setadmin 0]
    sleep 300 [name $temp_old_name]
]

//abanss (aban with remember for info and screenshot)
abanss = [
    temp_cn_vote = $arg1
    temp_old_name = (player1 name)
    temp_reason = $arg2
    if (< (strlen $arg2) 4) [
        temp_reason = "Fast reason (probably troller, hacker or ban evade)"
    ]

    info $temp_cn_vote
    name $admin_name
    goadmin
    sleep 200 [ban $temp_cn_vote $temp_reason]
    sleep 250 [toggleconsole]
    sleep 300 [screenshot]
    sleep 300 [setadmin 0]
    sleep 300 [name $temp_old_name]
    sleep 400 [toggleconsole]
]

//abanname (aban by the name)
abanname = [
    temp_cn_vote = (findcn $arg1)
    aban $temp_cn_vote $arg2
]

//abannamess (aban by the name)
abannamess = [
    temp_cn_vote = (findcn $arg1)
    abanss $temp_cn_vote $arg2
]

//set_fast_ban_name (to fastly ban trollers). Always starts empty (or you can fill here), but you can set ingame
fast_ban_name = ""

bind U [abanname $fast_ban_name]
set_fast_ban_name = [
    fast_ban_name = $arg1
]

//Menu for fast ban player name
newmenu "\f3Fast ban player name\f5"
menuitemtextinput "Name" $fast_ban_name [fast_ban_name = $arg1]
menuitem "\f2[Apply]" [update_fast_ban_player_menu]

update_fast_ban_player_menu = [
    closecurmenu
    delmenu "\f3Fast ban player name\f5"
    newmenu "\f3Fast ban player name\f5"
    menuitemtextinput "Name" $fast_ban_name [fast_ban_name = $arg1]
    menuitem "\f2[Apply]" [update_fast_ban_player_menu]
]

bind I [showmenu "\f3Fast ban player name\f5"]
